buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
//        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.34.0'
    }
}

apply plugin: 'java-library'
apply plugin: 'com.vanniktech.maven.publish'

def projectName = 'gand'
group 'com.github.tommyettinger'
version "$VERSION_NAME" // You can set the version in gradle.properties .

import com.vanniktech.maven.publish.JavaLibrary
import com.vanniktech.maven.publish.JavadocJar

mavenPublishing {
    configure(new JavaLibrary(new JavadocJar.Javadoc(), true))
}

plainJavadocJar.archiveBaseName.set(projectName)

[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

// Disable JDK 8's doclint
// http://blog.joda.org/2014/02/turning-off-doclint-in-jdk-8-javadoc.html
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc).tap {
            configureEach {
                // The -quiet is because of some sort of weird JDK JavaCompiler bug:
                // https://discuss.gradle.org/t/passing-arguments-to-compiler-and-javadoc/1661
                options.addStringOption('Xdoclint:none,-missing', '-quiet')
            }
        }
    }
}

compileJava {
    sourceCompatibility = 8
    targetCompatibility = 8
    if (JavaVersion.current().isJava9Compatible()) {
        options.release.set(8)
    }
}

compileTestJava {
    sourceCompatibility = 8
    targetCompatibility = 8
    if (JavaVersion.current().isJava9Compatible()) {
        options.release.set(8)
    }
}

apply plugin: 'idea'
idea.module.excludeDirs += [file("src/main/java/com/github/tommyettinger/emu/"), file("docs/")]
[compileJava, compileTestJava, javadoc]*.exclude("com/github/tommyettinger/emu")

// JavaDocs will be published inside the docs/ folder, which you can easily put on GitHub Pages in your repo settings.
// You may instead want to remove this line if frequent doc changes use up too much repo space.
javadoc.destinationDir = file('docs/apidocs')

jar {
    archiveBaseName.set(projectName)
    manifest {
        attributes 'Implementation-Title': projectName, 'Implementation-Version': archiveVersion
    }
}

repositories {
    mavenLocal()
    mavenCentral()
//    maven { url 'https://s01.oss.sonatype.org' }
    google()
//    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
//    maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
    // JitPack is a good repo to have if you depend on commit versions.
    // Having the below repo might cause problems if uploading to Maven Central.
    maven { url 'https://jitpack.io' }
}

dependencies {
    //noinspection GDXOutdatedVersionGradleGroovy
    api "com.badlogicgames.gdx:gdx:$gdxVersion"
    api "com.github.tommyettinger:crux:$cruxVersion"
    api "com.github.tommyettinger:gdcrux:$gdcruxVersion"
    testImplementation "org.apache.fory:fory-core:$foryVersion"
    testImplementation "com.github.tommyettinger:cringe:$cringeVersion"
    testImplementation "com.squidpony:squidcore:$squidSquadVersion"
    testImplementation "com.squidpony:squidgrid:$squidSquadVersion"
    testImplementation "com.squidpony:squidglyph:$squidSquadVersion"
    testImplementation "com.squidpony:squidplace:$squidSquadVersion"
//    testImplementation "com.github.yellowstonegames.squidsquad:squidcore:$squidSquadVersion"
//    testImplementation "com.github.yellowstonegames.squidsquad:squidgrid:$squidSquadVersion"
//    testImplementation "com.github.yellowstonegames.squidsquad:squidglyph:$squidSquadVersion"
//    testImplementation "com.github.yellowstonegames.squidsquad:squidplace:$squidSquadVersion"

    testImplementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
    testImplementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    testImplementation "junit:junit:4.13.2"
}
